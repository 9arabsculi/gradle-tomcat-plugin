dependsOnChildren()

allprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'
    version = '0.8'
    defaultTasks 'clean', 'build', 'uberjar'
}

subprojects {
    def compatibilityVersion = 1.5
    sourceCompatibility = compatibilityVersion
    targetCompatibility = compatibilityVersion
    configurations.compile.transitive = true

    repositories {
        mavenCentral()
    }

    dependencies {
        groovy 'org.codehaus.groovy:groovy:1.7.10'
        testCompile 'junit:junit:4.+'
    }

    ideaProject {
        javaVersion = '1.6'

        withXml { provider ->
            def node = provider.asNode()

            // Use GIT
            def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
            vcsConfig.mapping[0].'@vcs' = 'Git'

            // Set Gradle home
            def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
            gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
        }
    }
}

task uberjar(type: Jar, dependsOn: subprojects.assemble) {
    description = 'Creates Tomcat plugin uberjar'

    subprojects.each { project ->
        from({
            project.configurations.archives.allArtifactFiles.collect { zipTree(it) }
        })
    }

    manifest {
        attributes 'Implementation-Title': 'Gradle Tomcat plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version')
    }
}