allprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'
    version = '0.9.5'
    defaultTasks 'clean', 'build', 'uberjar'
}

subprojects {
    def compatibilityVersion = 1.5
    sourceCompatibility = compatibilityVersion
    targetCompatibility = compatibilityVersion
    configurations.compile.transitive = true

    repositories {
        mavenCentral()
    }

    dependencies {
        groovy 'org.codehaus.groovy:groovy:1.8.4'
        testCompile 'junit:junit:4.+'
    }
}

task uberjar(type: Jar, dependsOn: subprojects.assemble) {
    description = 'Creates Tomcat plugin uberjar'

    subprojects.each { project ->
        from({
            project.configurations.archives.allArtifacts.files.collect { zipTree(it) }
        })
    }

    manifest {
        attributes 'Implementation-Title': 'Gradle Tomcat plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0'
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}